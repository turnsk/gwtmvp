plugins {
  id 'java-library'
  id 'maven-publish'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'sk.turn'
version = '1.8.5'
archivesBaseName = 'gwtmvp'
description = 'GWT MVP Library'

sourceSets {
  main.java {
    srcDirs = ['src']
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

dependencies {
  def notTransitive = { transitive = false }
  compileOnly 'com.google.gwt:gwt-user:2.9.0', notTransitive
  compileOnly 'com.google.gwt:gwt-dev:2.9.0', notTransitive
}

jar {
  manifest {
    attributes 'Implementation-Version': archiveVersion
  }
  from sourceSets.main.java.srcDirs
}

javadoc {
  title project.description
  classpath = sourceSets.main.compileClasspath
  options.addStringOption("sourcepath", "")
  options.addStringOption("link", "http://www.gwtproject.org/javadoc/latest/")
  source sourceSets.main.java.srcDirs
  include '**/gwtmvp/client/**'
  exclude '**/*.html'
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId = project.archivesBaseName
      from components.java
    }
  }
}
