plugins {
  id 'java-library'
  id 'maven-publish'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
group = 'sk.turn'
version = '1.8.0'

sourceSets {
  main.java {
    srcDirs = ['src']
  }
}

dependencies {
  implementation 'com.google.gwt:gwt-user:2.9.0'
  compileOnly 'com.google.gwt:gwt-dev:2.9.0'
}

jar {
  archiveBaseName.set('gwtmvp')
  manifest {
    attributes 'Implementation-Version': archiveVersion.get()
  }
  from sourceSets.main.java.srcDirs
}

task myJavadoc(type: Javadoc) {
  title 'GWT MVP Library'
  classpath = sourceSets.main.runtimeClasspath
  options.addStringOption("sourcepath", "")
  options.addStringOption("link", "http://www.gwtproject.org/javadoc/latest/")
  source sourceSets.main.java.srcDirs
  include '**/gwtmvp/client/**'
  exclude '**/*.html'
}

task sourcesJar(type: Jar) {
  archiveBaseName.set('gwtmvp')
  archiveClassifier.set('sources')
  from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: myJavadoc) {
  archiveBaseName.set('gwtmvp')
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId = 'gwtmvp'
      from components.java
    }
  }
}
